plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'ninja.oxente'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    mockitoAgent
    integrationTestImplementation.extendsFrom implementation
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.session:spring-session-data-mongodb'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.82'
    implementation 'org.springframework.security:spring-security-crypto:6.5.6'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'
    implementation platform('org.testcontainers:testcontainers-bom:2.0.1')

    testImplementation 'org.mockito:mockito-core:5.14.0'
    mockitoAgent('org.mockito:mockito-core:5.14.0') {
        transitive = false
    }
    testImplementation 'org.hamcrest:hamcrest'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:4.21.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    integrationTestImplementation('org.testcontainers:testcontainers-mongodb')
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(Test).configureEach {
    jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test

    useJUnitPlatform()

    testLogging {
        events "passed"
    }
}

check.dependsOn integrationTest
